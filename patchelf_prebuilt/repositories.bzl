"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("@bazel_tools//tools/build_defs/repo:http.bzl", _http_archive = "http_archive")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "maybe")
load("//patchelf_prebuilt/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//patchelf_prebuilt/private:versions.bzl", "TOOL_VERSIONS")

def http_archive(name, **kwargs):
    maybe(_http_archive, name = name, **kwargs)

# WARNING: any changes in this function may be BREAKING CHANGES for users
# because we'll fetch a dependency which may be different from one that
# they were previously fetching later in their WORKSPACE setup, and now
# ours took precedence. Such breakages are challenging for users, so any
# changes in this function should be marked as BREAKING in the commit message
# and released only in semver majors.
# This is all fixed by bzlmod, so we just tolerate it for now.
def rules_patchelf_prebuilt_dependencies():
    # The minimal version of bazel_skylib we require
    http_archive(
        name = "bazel_skylib",
        sha256 = "bc283cdfcd526a52c3201279cda4bc298652efa898b10b4db0837dc51652756f",
        urls = [
            "https://github.com/bazelbuild/bazel-skylib/releases/download/1.7.1/bazel-skylib-1.7.1.tar.gz",
            "https://mirror.bazel.build/github.com/bazelbuild/bazel-skylib/releases/download/1.7.1/bazel-skylib-1.7.1.tar.gz",
        ],
    )

########
# Remaining content of the file is only used to support toolchains.
########
_DOC = "Fetch external tools needed for patchelf_prebuilt toolchain"
_ATTRS = {
    "patchelf_prebuilt_version": attr.string(mandatory = True, values = TOOL_VERSIONS.keys()),
    "platform": attr.string(mandatory = True, values = PLATFORMS.keys()),
}

def _patchelf_prebuilt_repo_impl(repository_ctx):
    url = "https://github.com/someorg/someproject/releases/download/v{0}/patchelf_prebuilt-{1}.zip".format(
        repository_ctx.attr.patchelf_prebuilt_version,
        repository_ctx.attr.platform,
    )
    repository_ctx.download_and_extract(
        url = url,
        integrity = TOOL_VERSIONS[repository_ctx.attr.patchelf_prebuilt_version][repository_ctx.attr.platform],
    )
    build_content = """# Generated by patchelf_prebuilt/repositories.bzl
load("@com_booking_rules_patchelf_prebuilt//patchelf_prebuilt:toolchain.bzl", "patchelf_prebuilt_toolchain")

patchelf_prebuilt_toolchain(
    name = "patchelf_prebuilt_toolchain",
    target_tool = select({
        "@bazel_tools//src/conditions:host_windows": "patchelf_prebuilt_tool.exe",
        "//conditions:default": "patchelf_prebuilt_tool",
    }),
)
"""

    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", build_content)

patchelf_prebuilt_repositories = repository_rule(
    _patchelf_prebuilt_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def patchelf_prebuilt_register_toolchains(name, register = True, **kwargs):
    """Convenience macro for users which does typical setup.

    - create a repository for each built-in platform like "patchelf_prebuilt_linux_amd64" -
      this repository is lazily fetched when node is needed for that platform.
    - TODO: create a convenience repository for the host platform like "patchelf_prebuilt_host"
    - create a repository exposing toolchains for each platform like "patchelf_prebuilt_platforms"
    - register a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.
    Args:
        name: base name for all created repos, like "patchelf_prebuilt1_14"
        register: whether to call through to native.register_toolchains.
            Should be True for WORKSPACE users, but false when used under bzlmod extension
        **kwargs: passed to each node_repositories call
    """
    for platform in PLATFORMS.keys():
        patchelf_prebuilt_repositories(
            name = name + "_" + platform,
            platform = platform,
            **kwargs
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
